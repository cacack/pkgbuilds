# Contributor: Jason St. John <jstjohn .. purdue . edu>

_perlmod=GraphViz2
pkgname=perl-graphviz2
pkgver=2.14
pkgrel=1
pkgdesc="GraphViz2 - A wrapper for AT&T's Graphviz"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('Artistic2.0')
options=('!emptydirs')
depends=('perl-algorithm-dependency>=1.110'
         'perl-capture-tiny>=1.10'
         'perl-class-isa>=0.36'
         'perl-class-load>=0.17'
         'perl-dbi>=1.616'
         'perl-file-temp>=0.22'
         'perl-file-which>=1.09'
         'perl-hash-fieldhash>=0.10'
         'perl-ipc-run>=0.90'
         'perl-log-handler>=0.71'
         'perl-parse-recdescent>=1.965001'
         'perl-text-xslate>=1.2000'
         'perl-tree-dag-node>=1.06'
         'perl-try-tiny>=0.09'
         'perl-xml-bare')
makedepends=('perl-module-build>=0.3800' 'perl-test-pod>=1.45')
source=("http://cpan.perl.org/modules/by-module/$_perlmod/$_perlmod-${pkgver}.tgz")
sha512sums=('a39915d003fccc99c2cc3b87e0d9039d79ce5ed433ff7b9a28b74130cf4d5e634f499ea7affefdc5cc99b4a4bc993276a34f12c2eeeb25e844d916e218eb1a1a')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	perl Build.PL installdirs=vendor destdir="$pkgdir/"
	perl Build
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	perl Build test
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	perl Build install
}
