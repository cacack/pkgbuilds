# Maintainer: Chris Clonch <chris at theclonchs dot com>
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# https://github.com/cacack/pkgbuilds/tree/master/perl-graphviz2

pkgname=perl-graphviz2
pkgver=2.34
pkgrel=1
pkgdesc="GraphViz2 - A wrapper for AT&T's Graphviz"
_dist=GraphViz2
arch=('any')
url="http://search.cpan.org/dist/$_dist"
license=('Artistic2.0')
options=('!emptydirs')
depends=(
         'graphviz'
         'perl'
         'perl-algorithm-dependency'
         'perl-capture-tiny'
         'perl-class-isa'
         'perl-class-load'
         'perl-data-section-simple'
         'perl-date-simple'
         'perl-dbi'
         'perl-dbix-admin-tableinfo'
         'perl-file-homedir'
         'perl-file-slurp'
         'perl-file-temp'
         'perl-file-which'
         'perl-html-entities-interpolate'
         'perl-html-tree'
         'perl-ipc-run3'
         'perl-lingua-en-pluraltosingular'
         'perl-log-handler'
         'perl-moo'
         'perl-parse-recdescent'
         'perl-perl6-slurp'
         'perl-set-array'
         'perl-text-xslate'
         'perl-tree-dag-node'
         'perl-try-tiny'
         'perl-xml-bare'
         'perl-xml-tiny'
         )
makedepends=(
             'perl-module-build'
             'perl-test-version'
             )
source=("http://search.cpan.org/CPAN/authors/id/R/RS/RSAVAGE/$_dist-$pkgver.tgz")
sha512sums=('c39c92f1530c468d35a216f5252ce6c927b61f030a83b39ca253dbc6153a6ebd4b30115bbf8b48cee728f888ef39786fc68c8adf367d98033544707473e7caac')

build() {
   cd "$srcdir/$_dist-$pkgver"
   unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
   export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
   /usr/bin/perl Makefile.PL
   make
}

check() {
   cd "$srcdir/$_dist-$pkgver"
   unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
   export PERL_MM_USE_DEFAULT=1
   make test
}

package() {
   cd "$srcdir/$_dist-$pkgver"
   unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
   make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
