# Maintainer: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-want
_cpanname="Want"
pkgver=0.23
pkgrel=1
pkgdesc="A generalisation of wantarray"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/$_cpanname"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.5.0')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/R/RO/ROBIN/$_cpanname-$pkgver.tar.gz")
md5sums=('0eb0140b709b0a707bee62080a1d1df8')
sha1sums=('0e391874860988cbdac2d5448ae66179e8b6e993')
sha256sums=('773d7eea27f3f49ac61982b0dd51778d70add38fc8daa8746beb628563bff723')
sha512sums=('e0b893a3a76d4d356ccb9bd8af650fdcbd1a837f8b51579e0eb74b531598ec2a8496dc5e27e36872464c06b1e9785baa9a19dda8056ecfeb9c650429beb2e81a')

# Function to change to the working directory and set
# environment variables to override undesired options.
prepareEnvironment() {
	cd "$srcdir/$_cpanname-$pkgver"
	export \
		PERL_MM_USE_DEFAULT=1 \
		PERL_AUTOINSTALL=--skipdeps \
		PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
		PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
		MODULEBUILDRC=/dev/null
}

build() {
	prepareEnvironment
	/usr/bin/perl Makefile.PL
	make
}

check() {
	prepareEnvironment
	make test
}

package() {
	prepareEnvironment
	make install

	# Remove "perllocal.pod" and ".packlist".
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
